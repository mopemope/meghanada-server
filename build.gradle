plugins {
    id 'java'
    id 'maven'
    id 'jacoco'
    id 'pmd'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'com.github.ben-manes.versions' version '0.13.0'
    id 'com.jfrog.bintray' version '1.7.1'
    id 'org.ajoberstar.grgit' version '1.5.1'
    id 'org.ensime.gradle' version '0.2.8'
}

ext {
    group = 'meghanada'
    serverVersion = '0.2.4'

    longVersion = "$serverVersion-${grgit.head().abbreviatedId}"
    date = new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    sourceCompatibility = 1.8

    gradleVersion = '3.1'
    log4jVersion = '2.7'
}

mainClassName = "meghanada.Main"
applicationName = 'meghanada'

shadowJar {
    baseName = 'meghanada'
    classifier = null
    version = serverVersion
}

repositories {
    mavenCentral()
    maven {
        url 'http://repo.gradle.org/gradle/libs-releases-local'
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    publish = true
    // override = true

    filesSpec {
        from 'build/libs'
        into '.'
    }

    pkg {
        repo = 'meghanada'
        name = 'meghanada'
        vcsUrl = 'https://github.com/mopemope/meghanada-server.git'
        licenses = ['GPL-3.0']
        githubRepo = 'mopemope/meghanada-server'
        githubReleaseNotesFile = 'README.md'
        labels = ['java', 'emacs']

        version {
            name = "$serverVersion"
            desc = "Meghanada Server $longVersion"
            released  = new Date()
        }
    }
}

dependencies {

    compile 'org.apache.maven:maven-model:3.3.9'
    compile 'com.leacox.motif:motif:0.1'
    compile 'com.leacox.motif:motif-hamcrest:0.1'
    compile 'com.github.javaparser:javaparser-core:2.5.1'
    compile "org.apache.logging.log4j:log4j-core:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-api:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
    compile 'commons-cli:commons-cli:1.3.1'
    compile "org.gradle:gradle-tooling-api:$gradleVersion"
    compile 'com.google.guava:guava:19.0'
    compile 'org.ow2.asm:asm:5.1'
    compile 'com.esotericsoftware:kryo:3.0.3'
    compile 'com.typesafe:config:1.3.1'
    compile 'org.atteo:evo-inflector:1.2.1'
    compile 'junit:junit:4.12'
}

test {
    jvmArgs = ["-XX:SoftRefLRUPolicyMSPerMB=50", "-XX:+UseConcMarkSweepGC", "-Xverify:none",  "-Xms256m", "-Xmx2G"]

    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        exceptionFormat "full"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "$gradleVersion"
}

task appVersion << {
    print longVersion
}

task installEmacsHome(type: Copy) {
    def home = System.properties['user.home']
    from "build/libs/meghanada-${serverVersion}.jar"
    into "$home/.emacs.d/meghanada/"
}

installEmacsHome.dependsOn(shadowJar)

task embedVersion(type: Copy) {
    from 'src/main/resources/VERSION'
    into 'build/resources/main'

    expand(buildDate: new Date(), version: longVersion, appName: applicationName)
}

classes.dependsOn embedVersion
embedVersion.mustRunAfter processResources

task createPom << {
    pom {
        project {
            groupId 'meghanada'
            artifactId 'meghanada'
            version "$serverVersion"
        }
    }.writeTo("pom.xml")
}